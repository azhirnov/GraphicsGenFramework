/*
	Shared source for initialization and updating particles.
*/

[HEADER]

#include "Common.glcs"
#import  "Compute/SimpleParticles/vertex.glcs"


void InitParticle (out Particle particle, const float globalTime);

void UpdateParticle (const Particle inParticle, out Particle outParticle,
					 const float frameTime, const float globalTime, const float dt);


//---------------------------------------------------------
[SOURCE]

struct GravityObject
{
	float3	position;
	float	gravity;
	float	radius;
};

struct MagneticObject
{
	float3	north;
	float3	south;
	float	induction;
};

const GravityObject		GRAVITY_OBJECTS[1] = {
	{ float3( 0.0 ), 0.1, 0.1 }
};

const MagneticObject	MAGNETIC_OBJECTS[1] = {
	{ float3( 0.0, 0.0, 0.1 ), float3( 0.0, 0.0, -0.1 ), 0.5 }
};

const AABBox	BOX				= { float3(-10.0), float3(10.0) };
const float		MAX_ITER		= 20.0;


void RestartParticle (out Particle particle, const float globalTime)
{
	real	index	= real(GetGlobalIndex());
	real	size	= real(GetGlobalIndexSize());
	real	vel		= 0.5;

	particle.position	= AABBox_GetPointInBox( BOX, float3( 0.1, 0.0, 0.0 ) );
	particle.size		= 8.0;
	particle.color		= 0xFFFFFFFF;
	particle.velocity	= ParticleEmitter_ConeVector( index, size ).zxy * -vel;
	particle.param.x	= Sign(ToSNorm( DHash12(float2( globalTime, index / size )) ));	// sign
}


void InitParticle (out Particle particle, const float globalTime)
{
	Particle_Create( particle );
	RestartParticle( particle, globalTime + GetGlobalIndexUNorm() * 1.6334 );
}


void UpdateParticle (const Particle inParticle, out Particle outParticle,
					 const float frameTime, const float globalTime, const float dt)
{
	outParticle = inParticle;

	const float cnt = Clamp( Round( frameTime / dt ), 1.0, MAX_ITER );

	for (float t = 0.0; t < cnt; ++t)
	{
		float3	accel		= float3( 0.0 );
		int		destroyed	= 0;
		float	sign		= outParticle.param.x;
		
		for (int i = 0; i < GRAVITY_OBJECTS.length; ++i)
		{
			accel		+= GravityAccel( outParticle.position, GRAVITY_OBJECTS[i].position, GRAVITY_OBJECTS[i].gravity );
			destroyed	+= int( distance( outParticle.position, GRAVITY_OBJECTS[i].position ) < GRAVITY_OBJECTS[i].radius );
		}
		
		for (int i = 0; i < MAGNETIC_OBJECTS.length; ++i) {
			accel += SphericalMagneticFieldAccel( outParticle.velocity, outParticle.position,
												  MAGNETIC_OBJECTS[i].north, MAGNETIC_OBJECTS[i].south,
												  MAGNETIC_OBJECTS[i].induction ) * sign;
		}

		outParticle.velocity += accel * dt;
		outParticle.position += outParticle.velocity * dt;

		if ( not AABBox_IsInside( BOX, outParticle.position ) or destroyed > 0 )
		{
			RestartParticle( outParticle, globalTime );
		}
	}

	outParticle.color = ParticleColor_FromVelocityLength( outParticle.velocity );
}
